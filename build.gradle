System.setProperty("file.encoding", "UTF-8")

buildscript {
    repositories {
        jcenter()
        gradlePluginPortal()
        maven {
            url "${System.properties['user.home']}/.m2/repository/"
        }

        maven {
            url "https://sandec.jfrog.io/artifactory/repo"
        }
    }

    dependencies {
        classpath 'org.openjfx:javafx-plugin:0.0.14'
        classpath "SANDEC:simplefx-plugin-gradle:$SIMPLEFX_VERSION"
        classpath "one.jpro:jpro-gradle-plugin:$JPRO_VERSION"
    }
}

allprojects {
    repositories {
        mavenLocal()
        jcenter()
        mavenLocal()

        maven {
            url "https://sandec.jfrog.io/artifactory/repo"
        }
    }

    apply plugin: 'java-library'
    apply plugin: 'de.sandec.simplefx'

    targetCompatibility = "11"
    sourceCompatibility = "11"
    compileScala.targetCompatibility = 1.8

    simplefx {
        scalaVersion = "$SCALA_VERSION"
        scalaVersionShort = "$SCALA_VERSION_SHORT"
    }

    group = 'SANDEC'
    version = "$JNODES_VERSION"

    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter-api:$JUNIT_VERSION"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$JUNIT_VERSION"
    }
}

configure([project(':example')]) {
    dependencies {
        implementation rootProject
        implementation project(':jnodes-devices')
        implementation project(':jnodes-config')

        // shouldn't be required, have to investigate!
        implementation group: 'org.scala-lang', name: 'scala-reflect', version: SCALA_VERSION
        implementation "org.scala-lang:scala-reflect:$SCALA_VERSION"
    }
    //apply plugin: 'java'
    //apply plugin: 'de.sandec.simplefx'
    apply plugin: 'application'
    apply plugin: 'jpro-gradle-plugin'

    //mainClassName = 'jnodes.example.StartScreenTest'
    mainClassName = 'jnodes.example.StartTestConfigurable'
    mainClassName = 'jnodes.example.StartTestCSS'
    mainClassName = 'jnodes.example.TestFontResizingText'

    jpro {
        if (project.hasProperty('JPRO_LICENSE_USERNAME')) {
            username     ="$JPRO_LICENSE_USERNAME"
            password     ="$JPRO_LICENSE_PASSWORD"
            licensenumber="$JPRO_LICENSE_LICENSENUMBER"
        }
    }

    apply plugin: 'org.openjfx.javafxplugin'

    javafx {
        version = "$JAVAFX_VERSION"
        modules = [ 'javafx.base', 'javafx.graphics', 'javafx.controls', 'javafx.fxml', 'javafx.media', 'javafx.web', 'javafx.swing' ]
    }
}

configure([rootProject, project(':jnodes-devices'), project(':jnodes-lang') ,project(':jnodes-config')]) {

    apply plugin: 'java'
    apply plugin: 'de.sandec.simplefx'
    apply plugin: 'maven-publish'

    simplefx {
        scalaVersion = "$SCALA_VERSION"
        scalaVersionShort = "$SCALA_VERSION_SHORT"
    }

    apply plugin: 'org.openjfx.javafxplugin'

    javafx {
        version = "$JAVAFX_VERSION"
        configurations = ["compileOnly", "testImplementation"]
        modules = [ 'javafx.base', 'javafx.graphics', 'javafx.controls', 'javafx.fxml', 'javafx.media', 'javafx.web', 'javafx.swing' ]
    }

    test {
        useJUnitPlatform()

        jvmArgs = ['-Dglass.platform=Monocle', '-Dmonocle.platform=Headless']
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                groupId project.group
                artifactId project.name
                version project.version
            }
        }
        repositories {
            maven {
                name = "artifactory"
                url = "https://sandec.jfrog.io/artifactory/repo"
                credentials {
                    username System.getenv("SANDEC_ARTIFACTORY_USERNAME")
                    password System.getenv("SANDEC_ARTIFACTORY_PASSWORD")
                }
            }
        }
    }

}

configure(rootProject) {
    compileJava {
        options.compilerArgs = [
                "--add-exports", "javafx.controls/com.sun.javafx.scene.control=de.sandec.jnodes",
                "--add-exports", "javafx.graphics/com.sun.javafx.scene.text=de.sandec.jnodes",
                "--add-exports", "javafx.controls/com.sun.javafx.scene.control.skin=de.sandec.jnodes",
                "--add-exports", "javafx.graphics/com.sun.javafx.scene=de.sandec.jnodes",
                "--add-exports", "javafx.controls/com.sun.javafx.scene.control.behavior=de.sandec.jnodes",
                "--add-exports", "javafx.graphics/com.sun.javafx.tk=de.sandec.jnodes",
                "--add-exports", "javafx.base/com.sun.javafx=de.sandec.jnodes",
                "--add-exports", "javafx.graphics/com.sun.javafx.geom=de.sandec.jnodes"
        ]
    }

    javadoc {
        options {
            encoding = 'UTF-8'
            version = true
            author = true
            addMultilineStringsOption("-add-exports").setValue([
                    "javafx.controls/com.sun.javafx.scene.control=de.sandec.jnodes",
                    "javafx.graphics/com.sun.javafx.scene.text=de.sandec.jnodes",
                    "javafx.controls/com.sun.javafx.scene.control.skin=de.sandec.jnodes",
                    "javafx.graphics/com.sun.javafx.scene=de.sandec.jnodes",
                    "javafx.controls/com.sun.javafx.scene.control.behavior=de.sandec.jnodes",
                    "javafx.graphics/com.sun.javafx.tk=de.sandec.jnodes",
                    "javafx.base/com.sun.javafx=de.sandec.jnodes",
                    "javafx.graphics/com.sun.javafx.geom=de.sandec.jnodes"
            ])
            addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

configure([project(':jnodes-devices')]) {
    dependencies {
        api rootProject
    }
}

configure([project(':jnodes-config')]) {
    dependencies {
        api rootProject
    }
}

configure([project(':jnodes-lang')]) {
    dependencies {
        api rootProject
        implementation "com.typesafe:config:1.4.2"
    }
}




